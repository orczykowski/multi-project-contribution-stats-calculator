plugins {
    id 'application'
    id 'jacoco'
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

apply plugin: "groovy"
apply plugin: "java"

group 'pl.boringstuff'
version '1.0.1'
sourceCompatibility = '17'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
    }
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
}

application {
    mainClass = 'pl.boringstuff.AppRunner'
}

run.with {
    it.args(safeGet("dateFrom"), safeGet("resultDir"), safeGet("repoPath"), safeGet("reportFormat"))
}

String safeGet(String name) {
    if (project.hasProperty(name)) {
        return "$name=${project.getProperty(name)}"
    }
    ""
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
    implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.8'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.1'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.30'
    implementation group: 'com.itextpdf', name: 'itextpdf', version: '5.5.13.2'
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.10.0.202012080955-r'

    testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.9', ext: 'pom'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.0-groovy-3.0'
    testImplementation group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '3.7.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

